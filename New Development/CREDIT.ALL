      *
      *    THIS ROUTINE IS USED TO ALLOCATE A DEBIT TO VARIOUS
      *    CREDIT TRANSACTIONS APPEARING ON THE CREDITORS ACCOUNT
      *    CLIN MUST CONTAIN THE LINE NUMBER ON WHICH THE TRANS-
      *    ACTIONS ARE TO BE DISPLAYED.
      *
       ALLOCATE-DEBIT	   SECTION.

       ALLOC-INIT.
             MOVE W90-CODE       TO W45-CODE.
	     OPEN OUTPUT CRETRN.
	     CLOSE CRETRN.
	     OPEN I-O CRETRN.
             MOVE W90-CR         TO W75-BALANCE.
	     ADD W90-DISCOUNT	 TO W75-BALANCE.
             MOVE W90-ALLOCATED  TO W75-ALLOCATED.
	     DISPLAY S-ALLOC.
	     MOVE ZERO		 TO WS-CRETRN W75-KEY WS-ERROR.
	     MOVE P-NUMBER	 TO CT-CRED.
	     MOVE ZERO		 TO CT-CRED
				    CT-SDTE
				    CT-CODE
				    CT-SEQ.
	     MOVE SPACES	 TO CT-SINV.
	     PERFORM START-AT-CTRN-KEY THRU READ-CRTRAN-EXIT.
	   IF WS-F-ERROR = 4
               GO TO ALLOC-COMPLETE.

       ALLOC-CRTRAN-NEXT.
	     PERFORM READ-CRTRAN-NEXT THRU READ-CRTRAN-EXIT.
	   IF (WS-F-ERROR = 4) OR
	      (CT-CRED NOT = P-NUMBER)
	       GO TO ALLOC-END-CRTRAN.
	   IF (CT-COST < ZERO) OR
	      (CT-COST = CT-PAID)
	       GO TO ALLOC-CRTRAN-NEXT.
	     MOVE CT-REC	 TO CRT-REC.
	     MOVE SPACE 	 TO CRT-UPDATE.
	     COMPUTE CRT-OUTSTD = CRT-VALUE - CRT-PAID.
	     MOVE ZERO		 TO CRT-ALLOC.
	     ADD 1		 TO WS-CRETRN W75-KEY.
	     PERFORM WRITE-CRETRN THRU WRITE-CRETRN-EXIT.
	   IF CRT-OUTSTD > W75-BALANCE
	       MOVE W75-BALANCE  TO CRT-ALLOC
           ELSE
	       MOVE CRT-OUTSTD	 TO CRT-ALLOC.
	     MOVE CT-CODE	 TO W90-CODE.
	     PERFORM GET-CREDITOR-TRAN-RECORD.

       ALLOC-DISP-TRAN.
	     DISPLAY S-CTRN AT LINE CLIN.
	   IF WS-ALLOCATE = "A"
	       MOVE "A" 	 TO WS-OPTION
	       GO TO ALLOC-CHK-SELECT.

       ALLOC-DISP-USER-OPT.
001020	     DISPLAY " " AT 2303 WITH FOREGROUND-COLOR 14
		     " view transactions, " WITH FOREGROUND-COLOR 7
                     "A" WITH FOREGROUND-COLOR 14
                     "ccept/" WITH FOREGROUND-COLOR 7
                     "C" WITH FOREGROUND-COLOR 14
                     "hange allocation, " WITH FOREGROUND-COLOR 7
                     "E" WITH FOREGROUND-COLOR 14
		     "nd or " WITH FOREGROUND-COLOR 7
                     "Esc" WITH FOREGROUND-COLOR 14
                     " to abort" WITH FOREGROUND-COLOR 7.

	ALLOC-GET-USER-OPT.
	     CALL X"AF" USING GET-SINGLE-CHAR, KEY-STATUS.
	     PERFORM CLEAR-L50.
	   IF ADIS-FUNC
	       EVALUATE KEY-CODE-1
		 WHEN UP-KEY
		     GO TO ALLOC-MOVE-UP
		 WHEN DOWN-KEY
		     GO TO ALLOC-MOVE-DOWN
		 WHEN ENTER-KEY
		 WHEN CR-KEY
		     GO TO ALLOC-CHK-SELECT
		 WHEN OTHER
		     CALL X"E5"
	       END-EVALUATE
	       GO TO ALLOC-GET-USER-OPT
	   ELSE
	   IF USER-FUNC
	       EVALUATE KEY-CODE-1
		 WHEN ESC-KEY
		     MOVE 9	 TO WS-ERROR
		     GO TO ALLOC-COMPLETE
		 WHEN OTHER
		     CALL X"E5"
	       END-EVALUATE
	       GO TO ALLOC-GET-USER-OPT
	   ELSE
	   IF DATA-8BIT
	       MOVE KEY-CODE-1X  TO WS-OPTION.

       ALLOC-CHK-SELECT.
	     CALL "CBL_TOUPPER" USING WS-OPTION
				BY VALUE WS-LENGTH
				RETURNING WS-STATUS.
	   IF NOT (WS-OPTION = "A" OR "C" OR "E")
               GO TO ALLOC-GET-USER-OPT.
           IF WS-OPTION = "C"
               GO TO ALLOC-CHANGE-AMT.
           IF WS-OPTION = "E"
               GO TO ALLOC-COMPLETE.
	   IF CRT-UPDATE = SPACE
	       IF CRT-ALLOC > W75-BALANCE
                   MOVE W75-BALANCE
				 TO CRT-ALLOC
               END-IF
	       ADD CRT-ALLOC	 TO W75-ALLOCATED
	       SUBTRACT CRT-ALLOC
                                 FROM W75-BALANCE
	       DISPLAY S-ALLOC
	       MOVE "Y" 	 TO CRT-UPDATE
	       PERFORM REWRITE-CRETRN THRU WRITE-CRETRN-EXIT.
           IF W75-BALANCE = ZERO
               GO TO ALLOC-END.
	   IF WS-CRETRN NOT < W75-KEY
	       GO TO ALLOC-CRTRAN-NEXT.
	     ADD 1		 TO WS-CRETRN
	     GO TO ALLOC-READ-CRETRN.

       ALLOC-MOVE-UP.
	   IF WS-CRETRN > 1
	       SUBTRACT 1	 FROM WS-CRETRN
	       GO TO ALLOC-READ-CRETRN.
	     GO TO ALLOC-DISP-USER-OPT.

       ALLOC-MOVE-DOWN.
	   IF WS-CRETRN NOT < W75-KEY
	       GO TO ALLOC-CRTRAN-NEXT.
	     ADD 1		 TO WS-CRETRN.
	     GO TO ALLOC-READ-CRETRN.

       ALLOC-CHANGE-AMT.
	   IF CRT-UPDATE = "Y"
	       ADD CRT-ALLOC	 TO W75-BALANCE
	       SUBTRACT CRT-ALLOC
                                 FROM W75-ALLOCATED
	       DISPLAY S-ALLOC
	       MOVE ZERO	 TO CRT-ALLOC
	       MOVE SPACE	 TO CRT-UPDATE
	       PERFORM REWRITE-CRETRN THRU WRITE-CRETRN-EXIT.
	     ACCEPT S-CTRN AT LINE CLIN.
	   IF CRT-ALLOC > CRT-OUTSTD
	       MOVE CRT-OUTSTD	 TO CRT-ALLOC.
	   IF CRT-ALLOC > W75-BALANCE
	       MOVE W75-BALANCE  TO CRT-ALLOC.
             GO TO ALLOC-DISP-TRAN.

       ALLOC-READ-CRETRN.
	     PERFORM READ-CRETRN THRU READ-CRETRN-EXIT.
             GO TO ALLOC-DISP-TRAN.

       ALLOC-END-CRTRAN.
             DISPLAY "No more transactions" AT 2541
		      WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 14.
	     PERFORM READ-CRTRAN-PREV THRU READ-CRTRAN-EXIT.
	     GO TO ALLOC-DISP-USER-OPT.

       ALLOC-END.
	   IF WS-ALLOCATE = "A"
	       GO TO ALLOC-COMPLETE.
	     DISPLAY "Total has been allocated " AT 2521
		      WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 1
		     "A" WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 14
		     "ccept/"
		      WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 1
		     "R" WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 14
		     "eview allocations > <"
		      WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 1.
             MOVE "A"            TO WS-OPTION.

       ALLOC-OPT.
             ACCEPT WS-OPTION AT 2573
		    WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 15
			 UPDATE AUTO.
	     CALL "CBL_TOUPPER" USING WS-OPTION
				BY VALUE WS-LENGTH
				RETURNING WS-STATUS.
	   IF NOT (WS-OPTION = "A" OR "R")
               GO TO ALLOC-OPT.
	     PERFORM CLEAR-L50.
           IF WS-OPTION = "R"
               GO TO ALLOC-DISP-TRAN.

       ALLOC-COMPLETE.
	     MOVE 1		 TO WS-CRETRN.
	     PERFORM CLEAR-L50.
             MOVE W45-CODE       TO W90-CODE.

       ALLOC-EXIT.
             EXIT.
