      /
      *     S T O C K / I N V E N T O R Y   L O O K U P	  R O U T I N E
      *
      *            PERFORMED FROM AA50 
      *            (P R O C E S S   F U N C T I O N   K E Y S)
      *
000010 STOCK-LOOKUP                   SECTION.

       STOCK-SAVE-LIN.
	     MOVE LIN		 TO SLIN.

000030 STOCK-LUP-SCREEN.
000040       DISPLAY CLR-SCREEN. 
000050       DISPLAY S-STK.
	     DISPLAY WS-RT-HD AT 0354
		     WITH FOREGROUND-COLOR 3 HIGHLIGHT.
	     DISPLAY "Single" AT 0368
		      WITH FOREGROUND-COLOR 3 HIGHLIGHT.
	     MOVE 1		 TO WS-PRICE.
      *
      *    ****    L O O K U P	 U S I N G   A L T E R N A T E	 C O D E
      *
000060	   IF W10-ETYPE = "A"
000070         DISPLAY S-STKA
000080     ELSE
      *
      *    ****    L O O K U P	 U S I N G   X - R E F	 C O D E
      *
000090	   IF W10-ETYPE = "X"
000100         DISPLAY S-STKC
000110     ELSE
      *
      *    ****    L O O K U P	 U S I N G   D E S C R I P T I O N
      *
000120	   IF W10-ETYPE = "D"
000130	       DISPLAY S-STKD
	   ELSE
      *
      *    ****    L O O K U P	 U S I N G   D E S C R I P T I O N   2
      *
	   IF W10-ETYPE = "2"
	       DISPLAY S-STKD2.
             MOVE SPACE          TO WS-OPTION.

       STOCK-LUP-CLR-SCREEN.
000140       MOVE 0601           TO CPOS.
000150       PERFORM STOCK-CRT-EDGE UNTIL CLIN = 22.
000160	     MOVE SPACES	 TO W10-ACODE W10-DESC W10-DESC2
000170				    W10-EXT-CODE2 W10-EXT-CODE1.

000200 STOCK-LUP-DATA.
      *
      *    ****    I T E M   C O D E   S E Q U E N C E
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё If an incomplete or invalid item code was entered by the  Ё
      *    Ё USER, then this code is used as the starting point in the Ё
      *    Ё stock / inventory lookup.				       Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	   IF W10-ETYPE = "Z"
               NEXT SENTENCE
           ELSE
      *
      *    ****    I T E M   C O D E   S E Q U E N C E
      *
000210	   IF W10-ETYPE = "I"
000220         ACCEPT SA-STK
000230     ELSE
      *
      *    ****    A L T E R N A T E   C O D E	 S E Q U E N C E
      *
000240	   IF W10-ETYPE = "A"
000250         ACCEPT SA-STKA
000260     ELSE
      *
      *    ****    X - R E F E R E N C E   C O D E   S E Q U E N C E
      *
000270	   IF W10-ETYPE = "X"
000280         ACCEPT SA-STKC
000290	   ELSE
      *
      *    ****    D E S C R I P T I O N   S E Q U E N C E
      *
	   IF W10-ETYPE = "D"
000300	       ACCEPT SA-STKD
	   ELSE
      *
      *    ****    D E S C R I P T I O N   2   S E Q U E N C E
      *
	       ACCEPT SA-STKD2.
	   IF USER-FUNC
	       EVALUATE KEY-CODE-1
		 WHEN ESC-KEY
000140		     MOVE SPACES TO W10-EXT-ITEM
000150		     GO TO STOCK-LOOKUP-EXIT
		 WHEN OTHER	 CALL X"E5"
	       END-EVALUATE
	       GO TO STOCK-LUP-DATA.

       STOCK-START-LOOKUP.
	     DISPLAY WS-BLNK78 AT 2302 WITH FOREGROUND-COLOR 3.
000340	     DISPLAY CLEAR-L25.
000350	     MOVE 6		 TO CLIN.
000360	     MOVE 1		 TO W75-S W75-S1 WS-P1 W75-P.

000380 STOCK-LUP-TAB-LOOP.
000390	     MOVE SPACES	 TO W75-EXT-CODE(W75-S1).
000400     IF W75-S1 < 18
000410	       ADD 1		 TO W75-S1
000420         GO TO STOCK-LUP-TAB-LOOP.
000430	     MOVE ZERO		 TO W75-S1.

000450 STOCK-LUP-ENTRY-TYPE.
      *
      *    ****    I T E M   C O D E   S E Q U E N C E
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё If no item code entered set to start from the first item  Ё
      *    Ё code in the file. (First item code in sort sequence)      Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
000460	   IF W10-ETYPE = "I" OR "Z"
000470	       IF W10-EXT-ITEM = SPACES
000480		   MOVE X"1F"	 TO W10-EXT-ITEM.
      *
      *    ****    A L T E R N A T E   C O D E	 S E Q U E N C E
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё If no alternate code entered set to start from the first  Ё	Ё
      *    Ё code in the file. (First alternate code in sort sequence) Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
000490	   IF W10-ETYPE = "A"
000500         IF W10-ACODE = SPACES
000510		   MOVE X"1F"	 TO W10-ACODE.
      *
      *    ****    X - R E F E R E N C E   C O D E   S E Q U E N C E
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё If no X-ref code entered set to start from the first X-refЁ
      *    Ё code in the file. (First X-ref code in sort sequence)     Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
000520	   IF W10-ETYPE = "X"
000530	       IF W10-EXT-CODE2 = SPACES
000540		   MOVE X"1F"	 TO W10-EXT-CODE2.
      *
      *    ****    D E S C R I P T I O N   S E Q U E N C E
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё If no description has been entered set to start from the  Ё
      *    Ё first description in the file. (First description in sort Ё
      *    Ё sequence)						       Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
000550	   IF W10-ETYPE = "D"
000560         IF W10-DESC = SPACES
000570		   MOVE X"1F"	 TO W10-DESC.
      *
      *    ****    2 n d   D E S C R I P T I O N   S E Q U E N C E
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё If no description has been entered set to start from the  Ё
      *    Ё first description in the file. (First description 2 in    Ё
      *    Ё sort sequence)					       Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
000550	   IF W10-ETYPE = "2"
000560	       IF W10-DESC2 = SPACES
000570		   MOVE X"1F"	 TO W10-DESC2.
      *
      *    ****    I T E M   C O D E   S E Q U E N C E
      *
000580	   IF W10-ETYPE = "I" OR "Z"
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё Convert the item code to upper case, as all keys in the   Ё
      *    Ё system are stored as upper case.			       Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
000590         PERFORM CA00 THRU CA06.
      *
      *    ****    A L T E R N A T E   C O D E	 S E Q U E N C E
      *
000600	   IF W10-ETYPE = "A"
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё Convert the alternate code to upper case, as all keys in  Ё
      *    Ё the system are stored as upper case.		       Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
000610	       PERFORM CA101 THRU CA105.
      *
      *    ****    X - R E F E R E N C E   C O D E   S E Q U E N C E
      *
000620	   IF W10-ETYPE = "X"
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё Convert the X-ref code to upper case, as all keys in the  Ё
      *    Ё system are stored as upper case.			       Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
000630	       PERFORM CA201 THRU CA205.
      *
      *    ****    D E S C R I P T I O N   S E Q U E N C E
      *
000640	   IF W10-ETYPE = "D"
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё Convert the description to upper case, as all keys in the Ё
      *    Ё system are stored as upper case.			       Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
000650	       MOVE W10-DESC	 TO W50-DATA
000660	       MOVE 30		 TO W50-LENGTH
	       CALL "CBL_TOUPPER" USING W50-30
				  BY VALUE W50-LENGTH
				  RETURNING WS-STATUS
000720	       MOVE W50-30	 TO W10-DESC.
      *
      *    ****    2 n d   D E S C R I P T I O N   S E Q U E N C E
      *
000640	   IF W10-ETYPE = "2"
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё Convert the description to upper case, as all keys in the Ё
      *    Ё system are stored as upper case.			       Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
000650	       MOVE W10-DESC2	 TO W50-DATA
000660	       MOVE 30		 TO W50-LENGTH
	       CALL "CBL_TOUPPER" USING W50-30
				  BY VALUE W50-LENGTH
				  RETURNING WS-STATUS
000720	       MOVE W50-30	 TO W10-DESC2.
000730	   IF W10-ETYPE = "I" OR "Z"
000740	       MOVE W10-EXT-ITEM TO STK-CODE
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё Set the starting point of lookup using the item code that Ё
      *    Ё has been selected. 				       Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
000750	       PERFORM START-AT-STOCK-CODE THRU READ-STOCK-EXIT
000760     ELSE
000770	   IF W10-ETYPE = "A"
000780	       MOVE W10-ACODE	 TO STK-ACODE
	       MOVE SPACES	 TO STK-CODE
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё Set the starting point of lookup using the alternate code Ё
      *    Ё that has been selected.				       Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
000790	       PERFORM START-AT-ALT-CODE THRU READ-STOCK-EXIT
000800     ELSE
000810	   IF W10-ETYPE = "X"
000820	       MOVE W10-EXT-CODE2
				 TO STK-XREF
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё Set the starting point of lookup using the X-ref code     Ё
      *    Ё that has been selected.				       Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
000830	       PERFORM START-AT-STOCK-XREF THRU READ-STOCK-EXIT
000840     ELSE
000850	   IF W10-ETYPE = "D"
000860	       MOVE W10-DESC	 TO STK-DESC
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё Set the starting point of lookup using the description    Ё
      *    Ё that has been selected.				       Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
000870	       PERFORM START-AT-STOCK-DESC THRU READ-STOCK-EXIT
000840     ELSE
000850	   IF W10-ETYPE = "2"
000860	       MOVE W10-DESC2	 TO STK-DESC2
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё Set the starting point of lookup using the description 2  Ё
      *    Ё that has been selected.				       Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
000870	       PERFORM START-AT-STOCK-DESC2 THRU READ-STOCK-EXIT.
      *
      *    ****   E N D   O F	F I L E   ?
      *
000880     IF WS-F-ERROR = 22
000890         GO TO STOCK-NO-RECORD.

000910 STOCK-GET-STOCK-REC.
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё	  Must the READ-STOCK-NEXT statement be skipped?       Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	   IF WS-OPTION = "J"
               MOVE SPACE        TO WS-OPTION
               GO TO STOCK-SKIP-STOCK-READ.
000920       PERFORM READ-STOCK-NEXT THRU READ-STOCK-EXIT.
000930     IF WS-F-ERROR = 22
000940	       SUBTRACT 1	 FROM W75-S
000950         GO TO STOCK-FULL-SCREEN.
      *
      *    ****   S T O R E   S T O C K   R E C O R D	P R I C E S
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё Store the item code and prices in an array which can then Ё
      *	   Ё be used to either access the record or to get the prices  Ё
      *    Ё that have been selected by the USER to be displayed.      Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
       STOCK-SKIP-STOCK-READ.
000960	     MOVE STK-CODE	 TO W75-EXT-CODE (W75-S).
	     MOVE 1		 TO W75-PACK.

       STOCK-PRICES-LOOP.
      *
      *    ****    P R I C E   P E R   (Each/Dozen/Pack/....)
      *
	     MOVE STK-PR(W75-PACK)
				 TO W75-PR(W75-S, W75-PACK).
      *
      *    ****    E X C L U S I V E   P R I C E   (Retail/...)
      *
	     MOVE STK-SL(W75-PACK)
				 TO W75-SL(W75-S, W75-PACK).
      *
      *    ****    E X C L U S I V E   P R I C E   (Cash/...)
      *
	     MOVE STK-CSH(W75-PACK)
				 TO W75-CSH(W75-S, W75-PACK).
      *
      *    ****    E X C L U S I V E   P R I C E   (Wholesale/...)
      *
	     MOVE STK-WSL(W75-PACK)
				 TO W75-WSL(W75-S, W75-PACK).
	   IF STK-TAX NOT = ZERO
      *
      *    ****    V A T   R A T E   A P P L I C A B L E
      *
	       MOVE W05-VAT (STK-TAX)
			     TO W05-RATE
      *
      *    ****    I N C L U S I V E   P R I C E   (Retail/...)
      *
	       COMPUTE W75-VSL(W75-S, W75-PACK)	ROUNDED
		     = W75-SL(W75-S, W75-PACK) +
		      (W75-SL(W75-S, W75-PACK) * W05-RTE)
      *
      *    ****    I N C L U S I V E   P R I C E   (Cash/...)
      *
	       COMPUTE W75-VCSH(W75-S, W75-PACK) ROUNDED
		     = W75-CSH(W75-S, W75-PACK)	+
		      (W75-CSH(W75-S, W75-PACK) * W05-RTE)
      *
      *    ****    I N C L U S I V E   P R I C E   (Wholesale/...)
      *
	       COMPUTE W75-VWSL(W75-S, W75-PACK) ROUNDED
		     = W75-WSL(W75-S, W75-PACK)	+
		      (W75-WSL(W75-S, W75-PACK) * W05-RTE)
	   ELSE
	       MOVE W75-SL(W75-S, W75-PACK)
				 TO W75-VSL(W75-S, W75-PACK)
	       MOVE W75-CSH(W75-S, W75-PACK)
				 TO W75-VCSH(W75-S, W75-PACK)
	       MOVE W75-WSL(W75-S, W75-PACK)
				 TO W75-VWSL(W75-S, W75-PACK).
      *
      *    ****    H A V E   A L L   P R I C E S   B E E N
      * 	   S T O R E D	 ?
      *
	   IF W75-PACK < 4
	       ADD 1		 TO W75-PACK
	       GO TO STOCK-PRICES-LOOP.
      *
      *    ****    U S E   D I F F E R E N T   C O L O U R S   F O R
      * 	   E A C H   S T O C K	 T Y P E
      *
       STOCK-FB-COL.
      * 					C O N T R O L E D
	   IF STK-IND = 0
	       MOVE "HIGHLIGHT FOREGROUND-COLOR  3"
				 TO  WS-CONT
	   ELSE
      * 					P R I C E D   I T E M
	   IF STK-IND = 1
	       MOVE "HIGHLIGHT FOREGROUND-COLOR  6"
				 TO  WS-CONT
	   ELSE
      * 					B U Y - O U T
	   IF STK-IND = 2
	       MOVE "HIGHLIGHT FOREGROUND-COLOR  7"
				 TO  WS-CONT
	   ELSE
      * 					H E A D I N G
	   IF STK-IND = 4
	       MOVE "HIGHLIGHT FOREGROUND-COLOR  2"
				 TO  WS-CONT
	   ELSE
      * 					L A B O U R
	       MOVE "HIGHLIGHT FOREGROUND-COLOR  5"
				 TO  WS-CONT.

       STOCK-DISP-DET.
	   IF WS-USE-ITM = "Y"
	       MOVE STK-CODE	 TO W30-ITM-CODE
	   ELSE
	   IF WS-EXT-STK = "Y"
	       MOVE STK-CODE	 TO W30-EXT-CODE
	   ELSE
	       MOVE STK-ITEM	 TO W30-ITEM.
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё	  Display the details of the stock/inventory item.     Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
000970	     DISPLAY (CLIN, 2) STK-CODE WITH CONTROL WS-CONT.
             MOVE STK-DESC       TO W10-DESC.
000980	     DISPLAY ( , 21) W10-SDSC WITH CONTROL WS-CONT.
	   IF STK-QUANT < ZERO
      *
      *    ****    N E G A T I V E   Q U A N T I T Y   ( -----9.99. )
      *
000990	       MOVE STK-QUANT  TO W100-NQNT
	   ELSE
	   IF STK-QUANT > 999999.99
      *
      *    ****    L A R G E   Q U A N T I T Y	 ( Z(06)9.9. )
      *
	       MOVE STK-QUANT  TO W100-LQNT
	   ELSE
      *
      *    ****    R E G U L A R   Q U A N T I T Y   ( Z(05)9.99. )
      *
	       MOVE STK-QUANT  TO W100-QNT.
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё If the record is not a  heading  record, then display the Ё
      *    Ё quantity, exclusive and inclusive prices.		       Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	   IF NOT (STK-IND = 4)
001000	       DISPLAY ( , 51) W100-QUANT WITH CONTROL WS-CONT
001010	       MOVE W75-SL(W75-S, W75-P)
				 TO W100-S6V2
001020	       DISPLAY ( , 61) W100-PRICE WITH CONTROL WS-CONT
001030	       MOVE W75-VSL(W75-S, W75-P)
				 TO W100-S6V2
001040	       DISPLAY ( , 71) W100-PRICE WITH CONTROL WS-CONT.
001050     IF CLIN < 21
001060	       ADD 1		 TO CLIN W75-S
001070         GO TO STOCK-GET-STOCK-REC.

001090 STOCK-FULL-SCREEN.
      *
      *    ****   U S E R   O P T I O N S
      *
001020	     DISPLAY "" AT 2302 WITH FOREGROUND-COLOR 14
		     X"1B1A" WITH FOREGROUND-COLOR 14
001030		     "," WITH FOREGROUND-COLOR 7
		     "PgUp" WITH FOREGROUND-COLOR 14
		     "," WITH FOREGROUND-COLOR 7
		     "PgDn" WITH FOREGROUND-COLOR 14
		     "," WITH FOREGROUND-COLOR 7
001040		     "ENTER" WITH FOREGROUND-COLOR 14
001050		     "," WITH FOREGROUND-COLOR 7
001170		     "Esc" WITH FOREGROUND-COLOR 14
		     "," WITH FOREGROUND-COLOR 7
		     "F2" WITH FOREGROUND-COLOR 14
		     "=View," WITH FOREGROUND-COLOR 7
		     "F5" WITH FOREGROUND-COLOR 14
		     "=Packs," WITH FOREGROUND-COLOR 7
		     "F6" WITH FOREGROUND-COLOR 14
		     "=Dsc2," WITH FOREGROUND-COLOR 7
		     "F7" WITH FOREGROUND-COLOR 14
		     "=Bin," WITH FOREGROUND-COLOR 7
		     "F8" WITH FOREGROUND-COLOR 14
		     "=Xref/alt,"
		      WITH FOREGROUND-COLOR 7
		     "F9" WITH FOREGROUND-COLOR 14
		     "=remks"
		      WITH FOREGROUND-COLOR 7.
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё   Price options (per pack size) that must be displayed.   Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
001170	     DISPLAY "1" AT 2502
		     WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 14
		     " " WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 1
		     WS-RT-HD WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 1
		     "," WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 1
		     "2" WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 14
		     " " WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 1
		     WS-CS-HD WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 1
		     "," WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 1
		     "3" WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 14
		     " " WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 1
		     WS-WS-HD WITH BACKGROUND-COLOR 3
				   FOREGROUND-COLOR 1.

       STOCK-POSITION-CURSOR.
	     PERFORM SAVE-SCREEN-2.
001220	     MOVE 0		 TO W75-S1.
001230       MOVE 0601           TO CPOS.

001250 STOCK-CRT-ATTRIB.
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё		  Restore the screen attributes.	       Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	     MOVE 1920		 TO W41-LENGTH.
	     CALL "CBL_WRITE_SCR_ATTRS" USING SAVE-POS
					      W42-ATTRIB2
					      W41-LENGTH
					RETURNING WS-STATUS.
001290	     MOVE 2000		 TO W41-LENGTH.

001310 STOCK-GET-USER-OPT.
      *
      *    ****   G E T   T H E   U S E R   R E S P O N S E
      *
	     CALL X"AF" USING GET-SINGLE-CHAR, KEY-STATUS.
	     DISPLAY SPACE AT 2540
		     WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 1.
	   IF ADIS-FUNC
	       EVALUATE KEY-CODE-1
		 WHEN UP-KEY
001320		     GO TO STOCK-ADJ-NO-KEYS
		 WHEN DOWN-KEY
001340		     GO TO STOCK-CHK-NO-KEYS
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё In addition to the price  options above, the USER is able Ё
      *    Ё to use the left or right arrow key to  show the different Ё
      *    Ё prices (per pack) while the details are displayed.        Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
		 WHEN LEFT-KEY
		     IF W75-P > 1
			 SUBTRACT 1  FROM W75-P
			 GO TO STOCK-CHK-PACKS
		 WHEN RIGHT-KEY
		     IF W75-P < 4
			 ADD 1	     TO W75-P
			 GO TO STOCK-CHK-PACKS
		 WHEN ENTER-KEY
		 WHEN CR-KEY
		     GO TO STOCK-CHK-SELECT
		 WHEN OTHER	 CALL X"E5"
	       END-EVALUATE
	       GO TO STOCK-GET-USER-OPT
	   ELSE
	   IF USER-FUNC
	       EVALUATE KEY-CODE-1
		 WHEN ESC-KEY
		     MOVE SPACES TO W10-EXT-ITEM
		     GO TO STOCK-LOOKUP-EXIT
		 WHEN PAGE-DOWN
001340		     GO TO STOCK-GET-NEXT-PAGE
		 WHEN PAGE-UP
001340		     GO TO STOCK-GET-PREV-PAGE
		 WHEN F2-KEY
		     PERFORM BA000
001510		     GO TO STOCK-GET-USER-OPT
		 WHEN F5-KEY
		     PERFORM BB000
001510		     GO TO STOCK-GET-USER-OPT
		 WHEN F6-KEY
		     MOVE WS1-CURS
				 TO CRT-LINE
		     PERFORM STOCK-DESC2 THRU STOCK-DESC2-EXIT
		     MOVE CRT-LINE
				 TO WS1-CURS
001510		     GO TO STOCK-GET-USER-OPT
		 WHEN F7-KEY
		     MOVE WS1-CURS
				 TO CRT-LINE
		     PERFORM STOCK-BIN THRU STOCK-BIN-EXIT
		     MOVE CRT-LINE
				 TO WS1-CURS
001510		     GO TO STOCK-GET-USER-OPT
		 WHEN F8-KEY
		     MOVE WS1-CURS
				 TO CRT-LINE
		     PERFORM STOCK-CODES THRU STOCK-CODES-EXIT
		     MOVE CRT-LINE
				 TO WS1-CURS
001510		     GO TO STOCK-GET-USER-OPT
		 WHEN F9-KEY
		     PERFORM STOCK-ADD-SPECS THRU STOCK-ADD-EXIT
		     PERFORM STOCK-FULL-SCREEN
001510		     GO TO STOCK-GET-USER-OPT
		 WHEN OTHER	 CALL X"E5"
	       END-EVALUATE
	       GO TO STOCK-GET-USER-OPT
	   ELSE
	   IF DATA-8BIT
	       MOVE KEY-CODE-1X  TO WS-OPTION
	       GO TO STOCK-CHK-PRICES.
	     CALL X"E5".
001280	     GO TO STOCK-GET-USER-OPT.

       STOCK-CHK-PACKS.
	   IF W75-P = 1
	       DISPLAY "Single" AT 0368
			WITH FOREGROUND-COLOR 3 HIGHLIGHT
	   ELSE
	   IF W75-P = 2
	       DISPLAY "Pack 1" AT 0368
			WITH FOREGROUND-COLOR 3 HIGHLIGHT
	   ELSE
	   IF W75-P = 3
	       DISPLAY "Pack 2" AT 0368
			WITH FOREGROUND-COLOR 3 HIGHLIGHT
	   ELSE
	   IF W75-P = 4
	       DISPLAY "Pack 3" AT 0368
			WITH FOREGROUND-COLOR 3 HIGHLIGHT.
	     GO TO STOCK-DISP-PRICES.

       STOCK-CHK-PRICES.
	     CALL "CBL_TOUPPER" USING WS-OPTION
				BY VALUE WS-LENGTH
				RETURNING WS-STATUS.
001500	   IF NOT (WS-OPTION = "1" OR "2" OR "3")
001510         GO TO STOCK-GET-USER-OPT.
	     MOVE WS-OPTION	 TO WS-PRICE.

       STOCK-DISP-PRICES.
	   IF WS-PRICE = "2"
               GO TO STOCK-CASH-PRICES.
	   IF WS-PRICE = "1"
               GO TO STOCK-RETAIL-PRICES.
	   IF WS-PRICE = "3"
               GO TO STOCK-WSALE-PRICES.
	     CALL X"E5".
001280	     GO TO STOCK-GET-USER-OPT.

       STOCK-GET-NEXT-PAGE.
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё Check that screen is full before trying to display the    Ё
      *    Ё next page. If screen is not full, then ignore the request Ё
      *    Ё and return to get the USER reponse.		       Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
001550	   IF W75-S < 16
001560         GO TO STOCK-GET-USER-OPT.

       STOCK-NEW-PAGE.
      *
      *    ****   D I S P L A Y	  A   B L A N K	  L O O K U P	F O R M
      *
001570	     MOVE 0601		 TO CPOS.
001580       PERFORM STOCK-CRT-EDGE UNTIL CLIN = 22.
001590       MOVE 6              TO CLIN.
001600       MOVE LOW-VALUES TO W75-KEYS.
001610	     MOVE 1		 TO W75-S W75-S1 W75-P.
001620       PERFORM STOCK-LUP-TAB-LOOP.
	     DISPLAY WS-RT-HD AT 0354 WITH FOREGROUND-COLOR 11.
001630       GO TO STOCK-GET-STOCK-REC.

       STOCK-GET-PREV-PAGE.
             MOVE 1              TO WS-IXS1.
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё Set WS-OPTION to bypass the READ-STOCK-NEXT in procedure  Ё
      *    Ё STOCK-GET-STOCK-REC.				       Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	     MOVE "J"		 TO WS-OPTION.

       STOCK-GET-PREV-LOOP.
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё If the USER has requested the previous screen, then read  Ё
      *    Ё backwards through the file to the first record of the     Ё
      *    Ё previous screen and then go to the procedure that is used Ё
      *    Ё to display the next page.				       Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	     PERFORM READ-STOCK-PREV THRU READ-STOCK-EXIT.
           IF WS-F-ERROR = 22
	       MOVE SPACE	 TO WS-OPTION
	       PERFORM STOCK-LUP-SCREEN THRU STOCK-LUP-CLR-SCREEN
	       GO TO STOCK-START-LOOKUP.
	   IF WS-IXS1 < 31
               ADD 1             TO WS-IXS1
               GO TO STOCK-GET-PREV-LOOP.
             GO TO STOCK-NEW-PAGE.

       STOCK-CASH-PRICES.
      *
      *    ****   D I S P L A Y	  C A S H   P R I C E S
      *
	     DISPLAY WS-CS-HD AT 0354 WITH FOREGROUND-COLOR 11.
             MOVE 1              TO W75-S2.
             MOVE 06             TO LIN.

       STOCK-CASH-LOOP.
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё If price not equal to zero then display the price or else Ё
      *    Ё display SPACES for pack 1 or N/A for packs other than 1.  Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	   IF NOT (W75-CSH(W75-S2, W75-P) = ZERO)
001010	       MOVE W75-CSH(W75-S2, W75-P)
				 TO W100-S6V2
001020	       DISPLAY (LIN, 61) W100-PRICE WITH FOREGROUND-COLOR 11
001010	       MOVE W75-VCSH(W75-S2, W75-P)
				 TO W100-S6V2
001040	       DISPLAY ( , 71) W100-PRICE WITH FOREGROUND-COLOR 11
	   ELSE
	   IF W75-P > 1
	       DISPLAY (LIN, 61) "   N/A   " WITH FOREGROUND-COLOR 11
	       DISPLAY ( , 71) "         " WITH FOREGROUND-COLOR 11
	   ELSE
	       DISPLAY (LIN, 61) "         " WITH FOREGROUND-COLOR 11.
           IF W75-S2 < W75-S
               ADD 1             TO W75-S2 LIN
               GO TO STOCK-CASH-LOOP.
             GO TO STOCK-SAVE-FULL-SCREEN.

       STOCK-RETAIL-PRICES.
      *
      *    ****   D I S P L A Y	  R E T A I L	P R I C E S
      *
	     DISPLAY WS-RT-HD AT 0354 WITH FOREGROUND-COLOR 11.
             MOVE 1              TO W75-S2.
             MOVE 06             TO LIN.

       STOCK-RETAIL-LOOP.
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё If price not equal to zero then display the price or else Ё
      *    Ё display SPACES for pack 1 or N/A for packs other than 1.  Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	   IF NOT (W75-SL(W75-S2, W75-P) = ZERO)
001010	       MOVE W75-SL(W75-S2, W75-P)
				 TO W100-S6V2
001020	       DISPLAY (LIN, 61) W100-PRICE WITH FOREGROUND-COLOR 11
001010	       MOVE W75-VSL(W75-S2, W75-P)
				 TO W100-S6V2
001040	       DISPLAY ( , 71) W100-PRICE WITH FOREGROUND-COLOR 11
	   ELSE
	   IF W75-P > 1
	       DISPLAY (LIN, 61) "   N/A   " WITH FOREGROUND-COLOR 11
	       DISPLAY ( , 71) "         " WITH FOREGROUND-COLOR 11
	   ELSE
	       DISPLAY (LIN, 61) "         " WITH FOREGROUND-COLOR 11.
           IF W75-S2 < W75-S
               ADD 1             TO W75-S2 LIN
               GO TO STOCK-RETAIL-LOOP.
             GO TO STOCK-SAVE-FULL-SCREEN.

       STOCK-WSALE-PRICES.
      *
      *    ****   D I S P L A Y	  W H O L E S A L E   P R I C E S
      *
	     DISPLAY WS-WS-HD AT 0354 WITH FOREGROUND-COLOR 11.
             MOVE 1              TO W75-S2.
             MOVE 06             TO LIN.

       STOCK-WSALE-LOOP.
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё If price not equal to zero then display the price or else Ё
      *    Ё display SPACES for pack 1 or N/A for packs other than 1.  Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	   IF NOT (W75-WSL(W75-S2, W75-P) = ZERO)
001010	       MOVE W75-WSL(W75-S2, W75-P)
				 TO W100-S6V2
001020	       DISPLAY (LIN, 61) W100-PRICE WITH FOREGROUND-COLOR 11
001010	       MOVE W75-VWSL(W75-S2, W75-P)
				 TO W100-S6V2
001040	       DISPLAY ( , 71) W100-PRICE WITH FOREGROUND-COLOR 11
	   ELSE
	   IF W75-P > 1
	       DISPLAY (LIN, 61) "   N/A   " WITH FOREGROUND-COLOR 11
	       DISPLAY ( , 71) "         " WITH FOREGROUND-COLOR 11
	   ELSE
	       DISPLAY (LIN, 61) "         " WITH FOREGROUND-COLOR 11.
           IF W75-S2 < W75-S
               ADD 1             TO W75-S2 LIN
               GO TO STOCK-WSALE-LOOP.
             GO TO STOCK-SAVE-FULL-SCREEN.

       STOCK-SAVE-FULL-SCREEN.
	     DISPLAY " " AT 2572 WITH BACKGROUND-COLOR 3
				      FOREGROUND-COLOR 1.
001990       PERFORM STOCK-CRT-ATTRIB.
001180	     MOVE 0		 TO W44-FUNCTION.
001190	     CALL X"B7" USING W44-FUNCTION W41-SAVE W43-SCREEN2.
           IF W75-S1 > 0
               GO TO STOCK-HIGHLIGHT.
             GO TO STOCK-GET-USER-OPT.

001650 STOCK-NO-RECORD.
      *
      *    ****   D I S P L A Y	  E R R O R   M E S S A G E
      *
001660	     DISPLAY "No record - " AT 2512
		      WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 1
001670		     "N" WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 14
001680		     "ext or "
		      WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 1
001690		     "Q" WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 14
001700		     "uit > <"
		      WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 1.

001720 STOCK-GET-USER-OPT-2.
      *
      *    ****   G E T   T H E   U S E R   R E S P O N S E
      *
001730	     ACCEPT WS-OPTION AT 2538
		    WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 15 AUTO.
	     CALL "CBL_TOUPPER" USING WS-OPTION
				BY VALUE WS-LENGTH
				RETURNING WS-STATUS.
001790	   IF NOT (WS-OPTION = "N" OR "Q")
001800         GO TO STOCK-GET-USER-OPT-2.
001810     IF WS-OPTION = "N"
001820         GO TO STOCK-LUP-SCREEN.
001830	     MOVE SPACES	 TO W10-EXT-ITEM.
001840	     GO TO STOCK-LOOKUP-EXIT.

001860 STOCK-ADJ-NO-KEYS.
001870     IF W75-S1 > 1
001880         SUBTRACT 1        FROM W75-S1 CLIN.
001890       GO TO STOCK-HIGHLIGHT.

001910 STOCK-CHK-NO-KEYS.
001920     IF W75-S1 = ZERO
001930         MOVE 1            TO W75-S1
001940         GO TO STOCK-HIGHLIGHT.
001950     IF W75-S1 < W75-S
001960         ADD 1             TO W75-S1 CLIN.

001980 STOCK-HIGHLIGHT.
001990       PERFORM STOCK-CRT-ATTRIB.
002000       DISPLAY W43-DET (CLIN) AT CPOS WITH REVERSE-VIDEO.
002010       GO TO STOCK-GET-USER-OPT.

002030 STOCK-CHK-SELECT.
002040     IF W75-S1 = ZERO
002050         GO TO STOCK-GET-USER-OPT.
002060	     MOVE CSTART	 TO WS1-CURSOR.
002070	     MOVE W75-EXT-CODE(W75-S1)
				 TO W10-EXT-ITEM.
002080       PERFORM READ-STOCK THRU READ-STOCK-EXIT.
002090	     MOVE ZERO		 TO COL-AND-LINE.
002100	     GO TO STOCK-LOOKUP-EXIT.

       STOCK-ADD-SPECS.
002040     IF W75-S1 = ZERO
002050         GO TO STOCK-ADD-EXIT.
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё Check if there is any additional specifications or other  Ё
      *    Ё information for this item in the memo file.	       Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
	     INITIALIZE SME-RECORD1.
002070	     MOVE W75-EXT-CODE(W75-S1)
				 TO SME-CODE W10-EXT-ITEM.
             MOVE 1              TO SME-SEQ.
             PERFORM READ-STKMEM THRU READ-STKMEM-EXIT.
           IF WS-F-ERROR NOT = ZERO
	       DISPLAY "No add. specifications" AT 2545
			WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 14
	       CALL X"AF" USING GET-SINGLE-CHAR, KEY-STATUS
               GO TO STOCK-ADD-EXIT.
             INITIALIZE W200-MEM-FLDS.

       STOCK-ADD-LOOP.
      *
      *    ****   D I S P L A Y   T H E   A D D I T I O N A L	D A T A
      *
	     MOVE SME-SEQ	 TO WS-S1.
             MOVE SME-DATA       TO W200-MEM (WS-S1).
             PERFORM READ-STKMEM-NEXT THRU READ-STKMEM-EXIT.
           IF WS-F-ERROR = ZERO
	       IF SME-CODE = W10-EXT-ITEM
                   GO TO STOCK-ADD-LOOP.
	     PERFORM SAVE-SCREEN-3.
	     MOVE 7		 TO SHADE-ROW.
	     MOVE 19		 TO SHADE-COL.
	     MOVE 60		 TO SHADE-WIDTH.
	     MOVE 15		 TO SHADE-LINES.
	     DISPLAY S-MEM.
	     PERFORM SCREEN-SHADOW.
	     DISPLAY SA-MEM.
021280	     DISPLAY "ENTER to continue" AT 2555
                      WITH FOREGROUND-COLOR 14.
	     CALL X"AF" USING GET-SINGLE-CHAR, KEY-STATUS.
	     PERFORM RESTORE-SCREEN-3.

       STOCK-ADD-EXIT.
             EXIT.

       STOCK-CODES.
	     PERFORM SAVE-SCREEN-3.
	     MOVE 1		 TO W75-S2.
	     MOVE 06		 TO CLIN LIN.
	     MOVE 51		 TO CCOL
	     DISPLAY S-CODES.
000150	     PERFORM STOCK-CDE-EDGE UNTIL CLIN = 22.
	     DISPLAY WS-BLNK78 AT 2302 WITH FOREGROUND-COLOR 3.
000340	     DISPLAY CLEAR-L25.
      *
      *    ****    D I S P L A Y   X - R E F   A N D   A L T   C O D E S
      *
       STOCK-CDE-DISP.
002070	     MOVE W75-EXT-CODE(W75-S2)
				 TO STK-CODE.
002080       PERFORM READ-STOCK THRU READ-STOCK-EXIT.
	     PERFORM STOCK-FB-COL.
	   IF NOT STK-IND = 4
	       DISPLAY (LIN, 51) STK-XREF WITH CONTROL WS-CONT
	       DISPLAY ( , 70) STK-ACODE WITH CONTROL WS-CONT.
	   IF W75-S2 < W75-S
               ADD 1             TO W75-S2 LIN
	       GO TO STOCK-CDE-DISP.
021280       DISPLAY "ENTER to continue" AT 2355
		      WITH FOREGROUND-COLOR 6 HIGHLIGHT.
	     CALL X"AF" USING GET-SINGLE-CHAR, KEY-STATUS.
	     PERFORM RESTORE-SCREEN-3.

       STOCK-CODES-EXIT.
             EXIT.
      *
      *    ****    D I S P L A Y   D E S C R I P T I O N   2
      *
       STOCK-DESC2.
	     PERFORM SAVE-SCREEN-3.
	     MOVE 1		 TO W75-S2.
	     MOVE 06		 TO LIN.
	     DISPLAY "Description-2" AT 0421 WITH REVERSE-VIDEO.
000340	     DISPLAY CLEAR-L25.

       STOCK-DSC-DISP.
002070	     MOVE W75-EXT-CODE(W75-S2)
				 TO STK-CODE.
002080       PERFORM READ-STOCK THRU READ-STOCK-EXIT.
	     PERFORM STOCK-FB-COL.
	   IF STK-DESC2 = SPACES
	       MOVE STK-DESC	 TO W10-DESC
	   ELSE
	       MOVE STK-DESC2	 TO W10-DESC.
	   IF NOT STK-IND = 4
	       DISPLAY (LIN, 21) W10-SDSC WITH CONTROL WS-CONT.
	   IF W75-S2 < W75-S
               ADD 1             TO W75-S2 LIN
	       GO TO STOCK-DSC-DISP.
021280	     DISPLAY "ENTER to continue" AT 2555
                      WITH FOREGROUND-COLOR 14.
	     CALL X"AF" USING GET-SINGLE-CHAR, KEY-STATUS.
	     PERFORM RESTORE-SCREEN-3.

       STOCK-DESC2-EXIT.
             EXIT.

002120 STOCK-CDE-EDGE.
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё Display individual lines to accommodate the x-ref and alt Ё
      *    Ё ernate codes from the line number up to and including the Ё
      *    Ё line number specified by the perform - varying / until    Ё
      *    Ё statement. Performed from procedure STOCK-CODES.	       Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
002130	     DISPLAY "                  Ё          " AT CPOS
		      WITH FOREGROUND-COLOR 3.
002140	     ADD 1		 TO CLIN.

       STOCK-BIN.
	     PERFORM SAVE-SCREEN-3.
	     MOVE 1		 TO W75-S2.
	     MOVE 06		 TO CLIN LIN.
	     MOVE 51		 TO CCOL
	     DISPLAY S-BIN.
000150	     PERFORM STOCK-BIN-EDGE UNTIL CLIN = 22.
	     DISPLAY WS-BLNK78 AT 2302 WITH FOREGROUND-COLOR 3.
000340	     DISPLAY CLEAR-L25.

       STOCK-BIN-DISP.
      *
      *    ****    D I S P L A Y   B I N   L O C A T I O N S
      *
002070	     MOVE W75-EXT-CODE(W75-S2)
				 TO STK-CODE.
002080       PERFORM READ-STOCK THRU READ-STOCK-EXIT.
	     PERFORM STOCK-FB-COL.
	   IF NOT STK-IND = 4
	       DISPLAY (LIN, 53) STK-BIN WITH CONTROL WS-CONT.
	   IF W75-S2 < W75-S
               ADD 1             TO W75-S2 LIN
	       GO TO STOCK-BIN-DISP.
021280       DISPLAY "ENTER to continue" AT 2355
                      WITH FOREGROUND-COLOR 14.
	     CALL X"AF" USING GET-SINGLE-CHAR, KEY-STATUS.
	     PERFORM RESTORE-SCREEN-3.

       STOCK-BIN-EXIT.
             EXIT.

002120 STOCK-BIN-EDGE.
002130	     DISPLAY "         " AT CPOS WITH FOREGROUND-COLOR 3.
002140	     ADD 1		 TO CLIN.

002120 STOCK-CRT-EDGE.
      *    зддддддддддддддддддддддддддддддддддддддддддддддддддддддддддд©
      *    Ё Display individual lines for the blank form - starting    Ё
      *    Ё from the line number, up to and including the line number Ё
      *    Ё specified by the perform - varying / until statement.     Ё
      *    Ё Performed from the following procedures STOCK-NEW-PAGE &  Ё
      *    Ё STOCK-LUP-CLR-SCREEN.				       Ё
      *    юддддддддддддддддддддддддддддддддддддддддддддддддддддддддддды
002130	     DISPLAY "Ё" AT CPOS WITH BACKGROUND-COLOR 0
				      FOREGROUND-COLOR 3
		     "                  Ё                             Ё
      -		     "        Ё         Ё         "
		      WITH FOREGROUND-COLOR 3
		     "Ё" WITH BACKGROUND-COLOR 0
			      FOREGROUND-COLOR 3.
002140	     ADD 1		 TO CLIN.

002160 STOCK-LOOKUP-EXIT.
002170       EXIT.
