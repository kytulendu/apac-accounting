      /
      *    ****    ROUTINES TO HANDLE VARIOUS FUNCTIONS FOR THE
      * 	   S C R E E N ,   K E Y B O A R D   &	 M O U S E
      *
       CRT-KBD-MOUSE   SECTION.
      *
      *    ****    S C R E E N   A T T R I B U T E S
      *
       SAVE-ATTRIB.
	     CALL "CBL_READ_SCR_ATTRS" USING SAVE-POS
					     W42-ATTRIB
					     W41-LENGTH
				       RETURNING WS-STATUS.

       SCREEN-ATTRIBUTES-4.
	     CALL "CBL_READ_SCR_ATTRS" USING SAVE-POS
					     W42-ATTRIB4
					     W41-LENGTH
				       RETURNING WS-STATUS.

       RESTORE-ATTRIB.
	     MOVE 2000		 TO W41-LENGTH.
	     CALL "CBL_WRITE_SCR_ATTRS" USING SAVE-POS
					      W42-ATTRIB4
					      W41-LENGTH
					RETURNING WS-STATUS.

       SCREEN-CONTENTS.
	    CALL "CBL_CLEAR_SCR" USING X"20" X"00"
				 RETURNING WS-STATUS.

       SAVE-SCREEN.
	     CALL "CBL_READ_SCR_CHATTRS" USING SAVE-POS
					       W43-SCREEN
					       W42-ATTRIB
					       W41-LENGTH
					 RETURNING WS-STATUS.

       SAVE-SCREEN-2.
	     CALL "CBL_READ_SCR_CHATTRS" USING SAVE-POS
					       W43-SCREEN2
					       W42-ATTRIB2
					       W41-LENGTH
					 RETURNING WS-STATUS.

       SAVE-SCREEN-3.
	     CALL "CBL_READ_SCR_CHATTRS" USING SAVE-POS
					       W43-SCREEN3
					       W42-ATTRIB3
					       W41-LENGTH
					 RETURNING WS-STATUS.

       RESTORE-SCREEN.
	     MOVE 2000		 TO W41-LENGTH.
	     CALL "CBL_WRITE_SCR_CHATTRS" USING SAVE-POS
						W43-SCREEN
						W42-ATTRIB
						W41-LENGTH
					  RETURNING WS-STATUS.

       RESTORE-SCREEN-2.
	     MOVE 2000		 TO W41-LENGTH.
	     CALL "CBL_WRITE_SCR_CHATTRS" USING SAVE-POS
						W43-SCREEN2
						W42-ATTRIB2
						W41-LENGTH
					  RETURNING WS-STATUS.

       RESTORE-SCREEN-3.
	     MOVE 2000		 TO W41-LENGTH.
	     CALL "CBL_WRITE_SCR_CHATTRS" USING SAVE-POS
						W43-SCREEN3
						W42-ATTRIB3
						W41-LENGTH
					  RETURNING WS-STATUS.

       POSITION-MOUSE.
	     MOVE 67		 TO MOUSE-FUNC.
	     CALL X"AF" USING MOUSE-FUNC MOUSE-DETAILS.
	     MOVE SCREEN-COL	 TO STORE-LIN
				    STORE-COL.
	     SUBTRACT 2 	 FROM STORE-LIN.
	     ADD STRING-LENGTH	 TO STORE-COL.
	   IF NOT (MOUSE-Y < TOP-ROW OR > BOTTOM-ROW)
	       IF NOT (MOUSE-X < STORE-LIN OR > STORE-COL)
		   IF MOUSE-Y = SCREEN-LIN
		       MOVE "Y"	 TO MOUSE-ENTER
		   ELSE
		       PERFORM CLEAR-BLOCK
		       MOVE MOUSE-Y
				 TO SCREEN-LIN
		       PERFORM SAVE-ATTR
		       PERFORM MARK-BLOCK.

       MOVE-BLOCK-UP.
	     PERFORM CLEAR-BLOCK.
	   IF SCREEN-LIN > TOP-ROW
	       SUBTRACT 1	 FROM SCREEN-LIN
	   ELSE
	       MOVE BOTTOM-ROW	 TO SCREEN-LIN.
	     PERFORM SAVE-ATTR.
	     PERFORM MARK-BLOCK.

       MOVE-BLOCK-DOWN.
	     PERFORM CLEAR-BLOCK.
	   IF SCREEN-LIN < BOTTOM-ROW
	       ADD 1		 TO SCREEN-LIN
	   ELSE
	       MOVE TOP-ROW	 TO SCREEN-LIN.
	     PERFORM SAVE-ATTR.
	     PERFORM MARK-BLOCK.

       CLEAR-BLOCK.
	     CALL "CBL_WRITE_SCR_ATTRS" USING SCREEN-POSITION
					      ORIGINAL-VID
					      STRING-LENGTH.

       MARK-BLOCK.
	     CALL "CBL_WRITE_SCR_ATTRS" USING SCREEN-POSITION
					      REVERSE-VID
					      STRING-LENGTH.

       SAVE-ATTR.
	     CALL "CBL_READ_SCR_ATTRS" USING SCREEN-POSITION
					     ORIGINAL-VID
					     STRING-LENGTH.
	     PERFORM SET-UP-REVERSE-VID
		     VARYING WS-S1 FROM 1 BY 1
		     UNTIL WS-S1 > STRING-LENGTH.

       SET-UP-REVERSE-VID.
	     MOVE ORIGINAL-CHAR (WS-S1)
				 TO W40-CHAR.
	     MOVE ZERO		 TO W40-N1 W40-N2.
	  IF W40-DEC > 127
	      SUBTRACT 128	 FROM W40-DEC
	      ADD 8		 TO W40-N2.
	  IF W40-DEC > 63
	      SUBTRACT 64	 FROM W40-DEC
	      ADD 4		 TO W40-N2.
	  IF W40-DEC > 31
	      SUBTRACT 32	 FROM W40-DEC
	      ADD 2		 TO W40-N2.
	  IF W40-DEC > 15
	      SUBTRACT 16	 FROM W40-DEC
	      ADD 1		 TO W40-N2.
	  IF W40-DEC > 7
	      SUBTRACT 8	 FROM W40-DEC
	      ADD 128		 TO W40-N1.
	  IF W40-DEC > 3
	      SUBTRACT 4	 FROM W40-DEC
	      ADD 64		 TO W40-N1.
	  IF W40-DEC > 1
	      SUBTRACT 2	 FROM W40-DEC
	      ADD 32		 TO W40-N1.
	  IF W40-DEC > 0
	      SUBTRACT 1	 FROM W40-DEC
	      ADD 16		 TO W40-N1.
	     ADD W40-N1 W40-N2	 GIVING W40-DEC.
	     MOVE W40-CHAR	 TO REVERSE-CHAR (WS-S1).

       SCREEN-SHADOW.
	     CALL "CBL_READ_SCR_ATTRS" USING SAVE-POS
					     W42-ATTRIB4
					     W41-LENGTH
				       RETURNING WS-STATUS.
	     ADD SHADE-COL SHADE-WIDTH
				 GIVING WS-S2.
	     ADD 1 WS-S2	 GIVING WS-S3.
	     ADD SHADE-LINES SHADE-ROW
				 GIVING WS-S4.
	     PERFORM SET-UP-SHADOW-VERT
		     VARYING WS-S1 FROM SHADE-ROW BY 1
		     UNTIL WS-S1 = WS-S4.
	     PERFORM SET-UP-SHADOW-HOR
		     VARYING WS-S2 FROM SHADE-COL BY 1
		     UNTIL WS-S2 > WS-S3.
	     CALL "CBL_WRITE_SCR_ATTRS" USING SAVE-POS
					      W42-ATTRIB4
					      W41-LENGTH
					RETURNING WS-STATUS.

       SET-UP-SHADOW-VERT.
	     MOVE SHADE-CHAR	 TO W42-CHR (WS-S1, WS-S2)
				    W42-CHR (WS-S1, WS-S3).

       SET-UP-SHADOW-HOR.
	     MOVE SHADE-CHAR	 TO W42-CHR (WS-S1, WS-S2).

       MESSAGE-INST.
	     DISPLAY CLEAR-L25.
001020	     DISPLAY "Use " AT 2524 WITH BACKGROUND-COLOR 3
					 FOREGROUND-COLOR 1
		     " " WITH FOREGROUND-COLOR 6 HIGHLIGHT
				BACKGROUND-COLOR 3
001030		     " to move the message window"
		      WITH BACKGROUND-COLOR 3
			   FOREGROUND-COLOR 1.

       HIDE-THE-CURSOR.
	     MOVE 255		 TO CUR-ROW CUR-COL.
	     CALL "CBL_SET_CSR_POS" USING CUR-CONT
				    RETURNING WS-STATUS.

       DISPLAY-THE-CURSOR.
	     MOVE 25		 TO CUR-ROW.
	     MOVE 01		 TO CUR-COL.
	     CALL "CBL_SET_CSR_POS" USING CUR-CONT
				    RETURNING WS-STATUS.

       CHECK-CORRECT   SECTION.
       CHECK-POS.
	     MOVE 13		 TO SLIN.

       CHECK-SETUP.
	     MOVE SPACE 	 TO WS-OPTION.
	     MOVE 54		 TO SCOL.
	     PERFORM SAVE-SCREEN.

       CHECK-DISPLAY.
	     ADD 1 SLIN 	 GIVING SHADE-ROW.
	     MOVE 26		 TO SHADE-COL.
	     MOVE 30		 TO SHADE-WIDTH.
	     MOVE 2		 TO SHADE-LINES.
	     DISPLAY S99 AT LINE SLIN.
	     PERFORM SCREEN-SHADOW.
	     PERFORM MESSAGE-INST.

       CHECK-REPLY.
	     ADD 1		 TO SLIN.
	     PERFORM HIDE-THE-CURSOR.
	     CALL X"AF" USING GET-SINGLE-CHAR, KEY-STATUS.
	   IF ADIS-FUNC
	       EVALUATE KEY-CODE-1
		 WHEN UP-KEY
		      PERFORM RESTORE-SCREEN
		      IF SLIN > 3
			  SUBTRACT 2 FROM SLIN
		      ELSE
			  MOVE 2     TO SLIN
		      END-IF
		      GO TO CHECK-DISPLAY
		 WHEN DOWN-KEY
		      PERFORM RESTORE-SCREEN
		      IF SLIN > 21
			  MOVE 21    TO SLIN
		      END-IF
		      GO TO CHECK-DISPLAY
		 WHEN ENTER-KEY  GO TO CHECK-UPPER
		 WHEN OTHER	 CALL X"E5"
	       END-EVALUATE
	       GO TO CHECK-REPLY
	   ELSE
	   IF DATA-8BIT
	       MOVE KEY-CODE-1X  TO WS-OPTION.

       CHECK-UPPER.
	     CALL "CBL_TOUPPER" USING WS-OPTION
				BY VALUE WS-LENGTH
				RETURNING WS-STATUS.
	     PERFORM DISPLAY-THE-CURSOR.
	     PERFORM RESTORE-SCREEN.

       CHECK-EXIT.
	     EXIT.

      *
      *    ****   T H I S   R O U T I N E   I S   U S E D   T O
      * 	  D I S P L A Y   E R R O R   M E S S A G E S
      *
      *    อออออออออออออออออออออออออออออออออออออออออออออออออออออออ
      *    To display an error message with " - Press ANY key" at
      *    the end; use PERFORM ERROR-MESSAGE.
      *
      *    To display just the error message; use PERFORM
      *    ERROR-LENGTH THRU ERROR-EXIT.
      *
      *    To display the error message higher or lower (default is
      *    line 13) firstly use PERFORM ERROR-SETUP THRU ERROR-POS
      *    or PERFORM ERROR-LENGTH THRU ERROR-POS. Move the line
      *    number to be used to SLIN and then PERFORM ERROR-DISPLAY
      *    THRU ERROR-EXIT.
      *
       ERROR-MESSAGE   SECTION.
       ERROR-SETUP.
	     MOVE SPACES	 TO WS-ERR-MES.
	     STRING WS-ERR-STRING DELIMITED BY "  "
		    " - Press ANY key" DELIMITED SIZE
		    INTO WS-ERR-MES.

       ERROR-LENGTH.
	     MOVE SPACE 	 TO WS-OPTION
	     MOVE 48		 TO WS-S1.

      * 			 FIND THE LENGTH OF THE MESSAGE
       ERROR-LOOP.
	   IF WS-ERR-CHAR (WS-S1) = SPACE
	       SUBTRACT 1	 FROM WS-S1
	       GO TO ERROR-LOOP.
	     SUBTRACT WS-S1 FROM 48
				 GIVING WS-COUNT.
	   IF WS-COUNT < 3
	       GO TO ERROR-POS.
	     DIVIDE 2		 INTO WS-COUNT.
	     SUBTRACT WS-COUNT FROM 48
				 GIVING WS-S2.

      * 		   CENTRE THE MESSAGE IN THE DISPLAY WINDOW
       ERROR-CENTRE.
	     MOVE WS-ERR-CHAR (WS-S1)
				 TO WS-ERR-CHAR (WS-S2).
	     MOVE SPACE 	 TO WS-ERR-CHAR (WS-S1).
	   IF WS-S1 > 1
	       SUBTRACT 1	 FROM WS-S1 WS-S2
	       GO TO ERROR-CENTRE.

       ERROR-POS.
	     MOVE 13		 TO SLIN.
	     MOVE 64		 TO SCOL.
	     PERFORM SAVE-SCREEN.

       ERROR-DISPLAY.
	     ADD 1 SLIN 	 GIVING SHADE-ROW.
	     MOVE 18		 TO SHADE-COL.
	     MOVE 48		 TO SHADE-WIDTH.
	     MOVE 2		 TO SHADE-LINES.
	     DISPLAY ERROR-LINE AT LINE SLIN.
	     PERFORM SCREEN-SHADOW.
	     PERFORM MESSAGE-INST.

       ERROR-REPLY.
	     ADD 1		 TO SLIN.
	     PERFORM HIDE-THE-CURSOR.
	     CALL X"AF" USING GET-SINGLE-CHAR, KEY-STATUS.
	   IF ADIS-FUNC
	       EVALUATE KEY-CODE-1
		 WHEN UP-KEY
		      PERFORM RESTORE-SCREEN
		      IF SLIN > 3
			  SUBTRACT 2 FROM SLIN
		      ELSE
			  MOVE 2     TO SLIN
		      END-IF
		      GO TO ERROR-DISPLAY
		 WHEN DOWN-KEY
		      PERFORM RESTORE-SCREEN
		      IF SLIN > 21
			  MOVE 21    TO SLIN
		      END-IF
		      GO TO ERROR-DISPLAY
		 WHEN ENTER-KEY  GO TO ERROR-END
		 WHEN OTHER	 CALL X"E5"
	       END-EVALUATE
	       GO TO ERROR-REPLY
	   ELSE
	   IF DATA-8BIT
	       MOVE KEY-CODE-1X  TO WS-OPTION.
	     CALL "CBL_TOUPPER" USING WS-OPTION
				BY VALUE WS-LENGTH
				RETURNING WS-STATUS.

       ERROR-END.
	     PERFORM DISPLAY-THE-CURSOR.
	     PERFORM RESTORE-SCREEN.

       ERROR-EXIT.
	     EXIT.
