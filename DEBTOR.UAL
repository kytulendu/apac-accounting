      *
      *    THIS ROUTINE IS USED TO UN-ALLOCATE A CREDIT FROM VARIOUS
      *    DEBIT TRANSACTIONS APPEARING ON THE DEBTORS ACCOUNT
      *    CLIN MUST CONTAIN THE LINE NUMBER ON WHICH THE TRANS-
      *    ACTIONS ARE TO BE DISPLAYED.
      *    
       UN-ALLOCATE-CREDIT     SECTION.

       UN-ALLOC-INIT.
             MOVE W90-CODE       TO W45-CODE.
             OPEN OUTPUT DEBTRN.
             CLOSE DEBTRN.
             OPEN I-O DEBTRN.
             MOVE W90-CR         TO W75-BALANCE.
             MOVE W90-ALLOCATED  TO W75-ALLOCATED.
	     DISPLAY S-ALLOC.
             MOVE ZERO           TO WS-DBTKEY W75-KEY WS-ERROR.
             MOVE DEB-ACNO       TO TRA-AC.
             MOVE ZERO           TO TRA-DATE TRA-CODE.
             MOVE SPACES         TO TRA-REF.
             PERFORM START-AT-DTRN-KEY THRU READ-DBTRAN-EXIT.
           IF WS-F-ERROR = 5
	       GO TO UN-ALLOC-COMPLETE.

       UN-ALLOC-DBTRAN-NEXT.
             PERFORM READ-DBTRAN-NEXT THRU READ-DBTRAN-EXIT.
           IF (WS-F-ERROR = 5) OR
              (DEB-ACNO NOT = TRA-AC)
	       GO TO UN-ALLOC-END-DBTRAN.
           IF (TRA-VALUE < ZERO) OR
              (TRA-VALUE = TRA-PAID)
	       GO TO UN-ALLOC-DBTRAN-NEXT.
           IF (W45-CODE = 17) AND
              (TRA-CODE NOT = 20)
	       GO TO UN-ALLOC-DBTRAN-NEXT.
             MOVE TRA-RECORD1    TO DBT-RECORD1.
             MOVE SPACE          TO DBT-UPDATE.
             COMPUTE DBT-OUTSTD = DBT-VALUE - DBT-PAID.
             MOVE ZERO           TO DBT-ALLOC.
             ADD 1               TO WS-DBTKEY W75-KEY.
             PERFORM WRITE-DEBTRN THRU WRITE-DEBTRN-EXIT.
           IF DBT-OUTSTD > W75-BALANCE
               MOVE W75-BALANCE  TO DBT-ALLOC
           ELSE
               MOVE DBT-OUTSTD   TO DBT-ALLOC.
             MOVE TRA-CODE       TO W90-CODE.
             PERFORM GET-DEBTOR-TRAN-DESC.

       UN-ALLOC-DISP-TRAN.
             DISPLAY S-DTRN AT LINE CLIN.

       UN-ALLOC-DISP-USER-OPT.
001020	     DISPLAY " " AT 2303 WITH FOREGROUND-COLOR 14
		     " view transactions, " WITH FOREGROUND-COLOR 7
                     "A" WITH FOREGROUND-COLOR 14
                     "ccept/" WITH FOREGROUND-COLOR 7
                     "C" WITH FOREGROUND-COLOR 14
                     "hange allocation, " WITH FOREGROUND-COLOR 7
                     "E" WITH FOREGROUND-COLOR 14
		     "nd or " WITH FOREGROUND-COLOR 7
                     "Esc" WITH FOREGROUND-COLOR 14
                     " to abort" WITH FOREGROUND-COLOR 7.

	UN-ALLOC-GET-USER-OPT.
	     CALL X"AF" USING GET-SINGLE-CHAR, KEY-STATUS.
	     DISPLAY CLEAR-L25.
	   IF ADIS-FUNC
	       EVALUATE KEY-CODE-1
		 WHEN UP-KEY
		     GO TO UN-ALLOC-MOVE-UP
		 WHEN DOWN-KEY
		     GO TO UN-ALLOC-MOVE-DOWN
		 WHEN ENTER-KEY
		 WHEN CR-KEY
		     GO TO UN-ALLOC-CHK-SELECT
		 WHEN OTHER
		     CALL X"E5"
	       END-EVALUATE
	       GO TO UN-ALLOC-GET-USER-OPT
	   ELSE
	   IF USER-FUNC
	       EVALUATE KEY-CODE-1
		 WHEN ESC-KEY
		     MOVE 9	 TO WS-ERROR
		     GO TO UN-ALLOC-COMPLETE
		 WHEN OTHER
		     CALL X"E5"
	       END-EVALUATE
	       GO TO UN-ALLOC-GET-USER-OPT
	   ELSE
	   IF DATA-8BIT
	       MOVE KEY-CODE-1X  TO WS-OPTION.

       UN-ALLOC-CHK-SELECT.
	     CALL "CBL_TOUPPER" USING WS-OPTION
				BY VALUE WS-LENGTH
				RETURNING WS-STATUS.
	   IF NOT (WS-OPTION = "A" OR "C" OR "E")
	       GO TO UN-ALLOC-GET-USER-OPT.
           IF WS-OPTION = "C"
	       GO TO UN-ALLOC-CHANGE-AMT.
           IF WS-OPTION = "E"
	       GO TO UN-ALLOC-COMPLETE.
           IF DBT-UPDATE = SPACE
               IF DBT-ALLOC > W75-BALANCE
                   MOVE W75-BALANCE
                                 TO DBT-ALLOC
               END-IF
               ADD DBT-ALLOC     TO W75-ALLOCATED
               SUBTRACT DBT-ALLOC
                                 FROM W75-BALANCE
	       DISPLAY S-ALLOC
	       MOVE "Y" 	 TO DBT-UPDATE
               PERFORM REWRITE-DEBTRN THRU WRITE-DEBTRN-EXIT.
           IF W75-BALANCE = ZERO
	       GO TO UN-ALLOC-END.
           IF WS-DBTKEY NOT < W75-KEY
	       GO TO UN-ALLOC-DBTRAN-NEXT.
             ADD 1               TO WS-DBTKEY
	     GO TO UN-ALLOC-READ-DEBTRN.

       UN-ALLOC-MOVE-UP.
	   IF WS-DBTKEY > 1
	       SUBTRACT 1	 FROM WS-DBTKEY
	       GO TO UN-ALLOC-READ-DEBTRN.
	     GO TO UN-ALLOC-DISP-USER-OPT.

       UN-ALLOC-MOVE-DOWN.
	   IF WS-DBTKEY NOT < W75-KEY
	       GO TO UN-ALLOC-DBTRAN-NEXT.
	     ADD 1		 TO WS-DBTKEY.
	     GO TO UN-ALLOC-READ-DEBTRN.

       UN-ALLOC-CHANGE-AMT.
           IF DBT-UPDATE = "Y"
               ADD DBT-ALLOC     TO W75-BALANCE
               SUBTRACT DBT-ALLOC
                                 FROM W75-ALLOCATED
	       DISPLAY S-ALLOC
	       MOVE ZERO	 TO DBT-ALLOC
	       MOVE SPACE	 TO DBT-UPDATE
               PERFORM REWRITE-DEBTRN THRU WRITE-DEBTRN-EXIT.
             ACCEPT S-DTRN AT LINE CLIN.
           IF DBT-ALLOC > DBT-OUTSTD
               MOVE DBT-OUTSTD   TO DBT-ALLOC.
           IF DBT-ALLOC > W75-BALANCE
               MOVE W75-BALANCE  TO DBT-ALLOC.
	     GO TO UN-ALLOC-DISP-TRAN.

       UN-ALLOC-READ-DEBTRN.
             PERFORM READ-DEBTRN THRU READ-DEBTRN-EXIT.
	     GO TO UN-ALLOC-DISP-TRAN.

       UN-ALLOC-END-DBTRAN.
             DISPLAY "No more transactions" AT 2541 
		      WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 14.
             PERFORM READ-DBTRAN-PREV THRU READ-DBTRAN-EXIT.
	     GO TO UN-ALLOC-DISP-USER-OPT.

       UN-ALLOC-END.
             DISPLAY "Total has been allocated " AT 2521
		      WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 1
		     "A" WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 14
		     "ccept/"
		      WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 1
		     "R" WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 14
		     "eview allocations > <"
		      WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 1.
             MOVE "A"            TO WS-OPTION.

       UN-ALLOC-OPT.
             ACCEPT WS-OPTION AT 2573 
		    WITH BACKGROUND-COLOR 3 FOREGROUND-COLOR 15
			 UPDATE AUTO.
	     CALL "CBL_TOUPPER" USING WS-OPTION
				BY VALUE WS-LENGTH
				RETURNING WS-STATUS.
	   IF NOT (WS-OPTION = "A" OR "R")
	       GO TO UN-ALLOC-OPT.
	     DISPLAY CLEAR-L25.
           IF WS-OPTION = "R"
	       GO TO UN-ALLOC-DISP-TRAN.

       UN-ALLOC-COMPLETE.
             MOVE 1              TO WS-DBTKEY.
	     DISPLAY CLEAR-L25.
             MOVE W45-CODE       TO W90-CODE.

       UN-ALLOC-EXIT.
             EXIT.
